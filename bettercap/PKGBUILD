# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.30
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'libpcap' 'libpcap.so' 'libnetfilter_queue' 'iproute2' 'libnfnetlink' 'libusb' 'libusb-1.0.so')
makedepends=('go' 'git')
optdepends=('bettercap-caplets: scripts (caplets) and proxy modules')
options=('!emptydirs')
source=(https://github.com/bettercap/bettercap/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('134b023783a1b15be76eaa045dae19b27272af12de378b39f066c06ae6e849ffafcdc415b420b7a69e50e202497502bfc7f803cddee1373f0ad710e964e6013a')
b2sums=('7dd8e6b0285c79cdc7d3e37a81759202819ff8f1b3bd10f1c9c77de6bebcd97b64b70376157821b1a4849a8a51df8774bd3e0281aba33fe9cf9a95edcd10861e')

prepare() {
  cd ${pkgname}-${pkgver}
  find . -type f -exec sed 's|/usr/local|/usr|g' -i {} +
}

build() {
  cd ${pkgname}-${pkgver}
  export GO11MODULE=on
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath"
  go build
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 bettercap.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
