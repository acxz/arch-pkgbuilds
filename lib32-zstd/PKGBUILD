# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

_pkgname=zstd
pkgname=lib32-zstd
pkgver=1.4.9
pkgrel=1
pkgdesc='Zstandard - Fast real-time compression algorithm (32-bit)'
url='https://facebook.github.io/zstd/'
arch=('x86_64')
license=('BSD' 'GPL2')
depends=('zstd' 'lib32-glibc')
makedepends=('gtest')
provides=('libzstd.so')
source=(https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.gz)
sha256sums=('29ac74e19ea28659017361976240c4b5c5c24db3b89338731a6feb97c038d293')
sha512sums=('10d325c844be43f801c798158c6f1d1ab57401abf1e783e04f6b9e4ac0ba53cf487204fa3244370b1ade239d5f3a784bf1829e206c4ba61fdd9c2f4e9502b238')
b2sums=('0bb27ce23166b01098222638859313ca5a07be41f5c1bf0ba096bd4525f42e05924c0676ea9ac6e52fc2bb9b6c5301baccd57ebe9895f4303a883999c4ae7a85')

build() {
  cd ${_pkgname}-${pkgver}
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  make
  make zstdmt
  make -C contrib/pzstd
}

check() {
  cd ${_pkgname}-${pkgver}
  make check
  make -C contrib/pzstd test
}

package() {
  cd ${_pkgname}-${pkgver}
  make \
    PREFIX=/usr \
    LIBDIR=/usr/lib32 \
    PKGCONFIGDIR=/usr/lib32/pkgconfig \
    DESTDIR="${pkgdir}" \
    install
  rm -rf "${pkgdir}"/usr/{share,include,bin}
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
