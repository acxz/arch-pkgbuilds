# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Santiago Torres-Arias <santiago[at]archlinux[dot]org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=sn0int
pkgver=0.21.0
pkgrel=1
pkgdesc='Semi-automatic OSINT framework and package manager'
url='https://github.com/kpcyrd/sn0int'
arch=('x86_64')
license=('GPL3')
depends=('libcap' 'lua52' 'sqlite' 'libseccomp.so' 'libsodium' 'geoip2-database' 'publicsuffix-list')
makedepends=('cargo' 'python-sphinx')
install=sn0int.install
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('88fe4e4f4bbb2b77ac3581855ff0544c92a77f6060f3b1e45b1870908bc95247f19e6cc83879e35e09b981301714233a6fc8d334f6f457355a193b38c32f0c3d'
            'SKIP')
b2sums=('7c61d3782f98411d4b1947228a445b7838954e58b5cc08233eefe1f8ef90959a78fec73776d208db73408dc6ce673fabd34fbdf3cc7afdb1e47099fa6a2cb0ad'
        'SKIP')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")


build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
  make -C docs man
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/sn0int" completions bash > "${pkgdir}/usr/share/bash-completion/completions/sn0int"
  "${pkgdir}/usr/bin/sn0int" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_sn0int"
  "${pkgdir}/usr/bin/sn0int" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/sn0int.fish"

  install -Dm 644 docs/_build/man/1/sn0int.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
