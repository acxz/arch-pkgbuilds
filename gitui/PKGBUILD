# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Simon Hauser <Simon-Hauser@outlook.de>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=gitui
pkgver=0.15.0
pkgrel=1
pkgdesc='Blazing fast terminal-ui for git written in Rust'
url='https://github.com/extrawurst/gitui'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'libgit2' 'libgit2.so' 'libxcb')
makedepends=('cargo' 'python') # xcb crate needs python
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('19476b4be4d383e316cc533f003a612f6d10caabda7ba757cfb3327b4210f923')
b2sums=('75773f16c5dcb82d17d9954b08fd2a72277b73f8a0fa8896ef56b139ab8177e31efd11d054af3730151c7e621e45c9c11cce4ac81633ac612500f469fed110b3')

build() {
  cd "${pkgname}-${pkgver}"
  export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/gitui -t "${pkgdir}/usr/bin/"
  install -Dm 644 {KEY_CONFIG,README,THEMES}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
