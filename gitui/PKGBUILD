# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Simon Hauser <Simon-Hauser@outlook.de>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=gitui
pkgver=0.13.0
pkgrel=1
pkgdesc='Blazing fast terminal-ui for git written in Rust'
url='https://github.com/extrawurst/gitui'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'libgit2' 'libgit2.so' 'libxcb')
makedepends=('cargo' 'python') # xcb crate needs python
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('ebf3ba559d05205e629805f60441411a0609e2a84444753cfd3a4a3ff1b98009')
b2sums=('840351d996ea82d655b7a99a1f740cce3465a0a7bda13808fffedd99e79bc11c6506f48d79b53340354e0f5ad1988044a85ba03869e8d0c50ce001b6a6579d23')

build() {
  cd "${pkgname}-${pkgver}"
  export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/gitui -t "${pkgdir}/usr/bin/"
  install -Dm 644 {KEY_CONFIG,README,THEMES}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
