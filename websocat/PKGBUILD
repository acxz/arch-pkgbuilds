# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: kpcyrd <git@rxv.cc>

pkgname=websocat
pkgver=1.7.0
pkgrel=1
pkgdesc='Command-line client for web sockets, like netcat/curl/socat for ws://'
url='https://github.com/vi/websocat/'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'openssl' 'gcc-libs')
makedepends=('cargo')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('9087abff708503d9682d2bbe43faa45365f1a0623f7106493271478943f836c011a742d1f194392b9e65890f06185704db6a27eae91814888378e7ad75c137e8')
b2sums=('b26bd47a911c87913c40ff1fd484fd833f292fa5c71bb43196a69c94243453c39bd61978dcd5f71fbf893a0edb1aa015780bc672ce39fabf5da263b820a56839')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked --features=ssl
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/websocat -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md doc.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
