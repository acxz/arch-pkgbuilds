# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=arch-audit
pkgver=0.1.18
pkgrel=1
pkgdesc='A utility like pkg-audit based on Arch Security Team data'
url='https://gitlab.com/ilpianista/arch-audit'
depends=('glibc' 'gcc-libs' 'curl' 'libcurl.so' 'libalpm.so')
makedepends=('cargo' 'pkg-config' 'scdoc')
arch=('x86_64')
license=('MIT')
backup=('etc/arch-audit/settings.toml')
source=("https://gitlab.com/ilpianista/arch-audit/-/archive/${pkgver}/arch-audit-${pkgver}.tar.gz")
sha512sums=('0a89ad9f6785b7468df8908165d2a0e77755582e475685d733463ffded156a1b9d639feed6480bf614c573b43a364d2c79e93b43a9c92e22d7b0f345705c6410')
b2sums=('d0aa7603ef14bd996ff1a97519352201165b083e1385c5c241cceec2dbc988d6d918eb345c41c380fd7e3572e093b47a56f57c13a3d60a80c21ad005ef152f96')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" PREFIX=/usr install

  install -Dm 644 contrib/hooks/arch-audit.hook -t "${pkgdir}/usr/share/${pkgname}"
  install -Dm 644 contrib/config/settings.toml -t "${pkgdir}/etc/arch-audit"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
