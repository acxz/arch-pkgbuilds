# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=hcxtools
pkgver=6.1.6
pkgrel=1
pkgdesc='Portable solution for capturing wlan traffic and conversion to hashcat and John the Ripper formats'
url='https://github.com/ZerBea/hcxtools'
arch=('x86_64')
license=('MIT')
depends=('curl' 'libcurl.so' 'libpcap' 'zlib' 'openssl')
source=(https://github.com/ZerBea/hcxtools/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/ZerBea/hcxtools/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('e2c495f1900bbaa338e365e7aa3f4e61a09601fd7a3167c88f1fc93a50d5cd96676b0bee70d026029be2b5a2257f798f4f0963e74888c4c87349e7da320e8d17'
            'SKIP')
validpgpkeys=('5920CE1C567948AFD2C0A9B7375516A45DB88630') # ZeroBeat <ZeroBeat@gmx.de>

build() {
  cd ${pkgname}-${pkgver}
  make PRODUCTION=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr PRODUCTION=1 install
  install -Dm 644 README.md changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
