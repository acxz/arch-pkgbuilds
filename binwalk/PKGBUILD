# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Peter Wu <peter@lekensteyn.nl>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: s1gma <s1gma@mindslicer.com>

pkgname=binwalk
pkgver=2.3.0
pkgrel=1
pkgdesc="A tool for searching a given binary image for embedded files"
url="https://github.com/ReFirmLabs/binwalk"
arch=('any')
license=('MIT')
depends=('python')
optdepends=(
  'python-opengl: binviz module support'
  'python-capstone: disassembly support'
  'arj: ARJ decompression support'
  'cabextract: CAB archive support'
  'cpio: CPIO archvie support'
  'gzip: GZIP decompression support'
  'mtd-utils: JFFS filesystem support'
  'p7zip: ZIP, LZMA and ISO decompression support'
  'squashfs-tools: squashfs support'
  'tar: TAR archive support'
  'bzip2: BZIP2 archive support'
  'unrar: RAR decompression support'
  'xz: XZ decompression support'
  'lhasa: LHA support'
  'sleuthkit: forensic analysis support'
)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/ReFirmLabs/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('74c25858eccaeff1bbf238cdff92df4cbec37dfff77204739bd82c35e4fd2329a8d0b13850b8b05900f756a99b898ebbbfb8ac44a26f37ca3ffb16c72402f9d6')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --prefix="${pkgdir}/usr"
  install -Dm 644 *.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
