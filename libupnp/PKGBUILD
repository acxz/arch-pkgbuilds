# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

_name=pupnp
pkgname=libupnp
pkgver=1.14.6
pkgrel=1
pkgdesc='Portable Open Source UPnP Development Kit'
url="https://pupnp.sourceforge.io/"
arch=('x86_64')
license=('BSD')
depends=('glibc')
provides=('libixml.so' 'libupnp.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/release-${pkgver}.tar.gz")
sha512sums=('66c8a537813367ba0ba67ef4097d065b31f4cfefbad9d60244c22a77357126575ee9211cfa42e7058f4d5fa8b0cacc7d011e82c4071361774e6ee3b9637b740f')
b2sums=('7d9e11dc982ffdaec70064aa029a92c2be8bdc03589d87769fa8c7702ea1c4e64654c3c5493d1494f81f6dfc83b3c17625b48a7cdc8566d493097230aaa8a052')

prepare() {
  mv -v ${_name}-release-${pkgver} ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
