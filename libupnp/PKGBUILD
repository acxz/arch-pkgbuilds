# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

_name=pupnp
pkgname=libupnp
pkgver=1.14.5
pkgrel=1
pkgdesc='Portable Open Source UPnP Development Kit'
url="https://pupnp.sourceforge.io/"
arch=('x86_64')
license=('BSD')
depends=('glibc')
provides=('libixml.so' 'libupnp.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/release-${pkgver}.tar.gz")
sha512sums=('6a1587943fb76b860fa879b050ae46189cb9c6a514df3f00eefe8c3eaa268aa5c0307895b38cac22fac57a2918b925ecefde7c46b7c50f87cfcf86afd9ee180d')
b2sums=('7b1a4f115d4fb545b26909d8f6feb0a017160876506f35a5932a0898e68104eb43d92f82872359bc7bccf27a05d41632296041bb450e8109758a0ff6b7dd8e58')

prepare() {
  mv -v ${_name}-release-${pkgver} ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
