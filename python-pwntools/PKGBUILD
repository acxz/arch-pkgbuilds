# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ding Xiao <tinocodfcdsa10@mails.tsinghua.edu.cn>
# Contributor: Firmy <firmianay@gmail.com>

pkgname=python-pwntools
_pyname=pwntools
pkgver=4.4.0
pkgrel=1
pkgdesc='CTF framework and exploit development library'
url='https://github.com/Gallopsled/pwntools'
arch=('any')
license=('MIT' 'GPL2' 'BSD')
depends=('python'
         'python-mako'
         'python-pyelftools'
         'python-capstone'
         'ropgadget'
         'python-pyserial'
         'python-requests'
         'python-pip'
         'python-pygments'
         'python-pysocks'
         'python-dateutil'
         'python-packaging'
         'python-psutil'
         'python-intervaltree'
         'python-sortedcontainers'
         'python-unicorn'
         'python-paramiko'
         'ropgadget')
makedepends=('pandoc' 'python-setuptools')
source=(https://github.com/Gallopsled/pwntools/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        pwn.conf)
sha256sums=('d6eb197a3173fc66e3479b65211d002951bd719215063e5bd7a2af6c767d2b76'
            '50916e4e45d026422cd6bfe8de50e7246c61b0b1e0007571e7c04a994282fe89')
b2sums=('bb9f313f17f8c43df2387db6f47f7337a8c615419f5d6e99dfb447782a7189a60d4dc012beb90c2419c370aa083cbea0810d6518f181d88dcc8c9e99ff3c21df'
        'cac0a12eea8abd3d2ee21632e7f2281e7e35c2d7839cfbcba83ae5d0e8f427e9cc2462a6d17d187252b99d9dbbb14393b9d5a63996c42043f553e9b66bfe9e07')

prepare() {
  cd ${_pyname}-${pkgver}
  sed 's|>=1.0.2rc1,<1.0.2rc4||' -i setup.py
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd "${_pyname}-${pkgver}"
  PYTHONPATH=. PWNLIB_NOTERM=true python -c 'import pwn'
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build --only-use-pwn-command
  install -Dm 644 "${srcdir}/pwn.conf" -t "${pkgdir}/etc"
  install -Dm 644 LICENSE-pwntools.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -f "${pkgdir}"/usr/lib/python*/site-packages/*.{txt,md}
}

# vim:set ts=2 sw=2 et:
