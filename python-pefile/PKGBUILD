# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>
# Contributor: Sonic-Y3k <sonic.y3k@googlemail.com>

pkgname=python-pefile
pkgver=2019.4.18
pkgrel=1
pkgdesc='Python PE parsing module'
url='https://github.com/erocarrera/pefile'
arch=('any')
license=('MIT')
depends=('python' 'python-future')
source=(https://github.com/erocarrera/pefile/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('351e9fa45731134b4c983b987ab865aa838d58c0c68b9d1a0645c705cd49d51c763ab6db7d8f5d0bf9a1ba8ea89dd297a7fc5bbd1d8184eebd84cf6553743ce5')
b2sums=('a9204309c6c4fc203830e948885811841e7355720affbeccaaf7be1110a391d18001ce610cdc8f34655dfe4836893742b42aa85be3694433ee6608b214363767')

build() {
  cd pefile-${pkgver}
  python setup.py build
}

package_python-pefile() {
  cd pefile-${pkgver}
  python setup.py install --root "${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README* -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
