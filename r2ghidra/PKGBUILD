# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=r2ghidra
_gitcommit=81cf63a8ef3aafcc67248ab952a454e69199df3f
pkgver=5.2.0
pkgrel=1
pkgdesc='Deep ghidra decompiler integration for radare2 and iaito'
url='https://github.com/radareorg/r2ghidra'
arch=('x86_64')
license=('LGPL3')
depends=('radare2' 'glibc')
makedepends=('git' 'cmake')
provides=('radare2-r2ghidra')
replaces=('r2ghidra-dec')
source=("git+https://github.com/radareorg/r2ghidra#commit=${_gitcommit}"
        radare-ghidra::git+https://github.com/radareorg/ghidra.git
        git+https://github.com/zeux/pugixml.git)
sha512sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule."third-party/pugixml".url "${srcdir}/pugixml"
  git config submodule."ghidra/ghidra".url "${srcdir}/radare-ghidra"
  git submodule update --recursive
}

build() {
  cd ${pkgname}
  cmake \
    -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SLASPECS=ON
  make -C build VERBOSE=1
}

package() {
  cd ${pkgname}
  make -C build -j1 DESTDIR="${pkgdir}" install
  # move shared library into R2_LIBR_PLUGINS
  r2version=$(r2 -version|grep radare2|sed -E 's/radare2 ([0-9\.]+) .*/\1/')
  install -d "${pkgdir}/usr/lib/radare2"
  mv "${pkgdir}/usr/lib/radare2/last" "${pkgdir}/usr/lib/radare2/${r2version}"
  ln -s /usr/lib/radare2/${r2version} "${pkgdir}/usr/lib/radare2/last"
}

# vim: ts=2 sw=2 et:
