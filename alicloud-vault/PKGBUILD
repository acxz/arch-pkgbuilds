# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=alicloud-vault
pkgver=1.3.3
pkgrel=1
pkgdesc='Vault for securely storing and accessing Alibaba Cloud credentials in development environments'
url='https://github.com/arafato/alicloud-vault'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('go')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c7dfca897758481d8085e93d89ab7e97b94c71edf6f11ac7e4fd402679d29fd845190f52bfacff08d66da8df7f6dd4f1319088001ca2699e687ff5166a80d81e')
b2sums=('813eb2154bac2371ff4e7edcb3ddb617c1bc88069cea77bb5e22e428a1f89cce01885b26b6cd89a167cf7f379ce3967e7b3360526ed59f8806b4706514f8da00')

build() {
  cd ${pkgname}-${pkgver}
  export GO11MODULE=on
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath"
  go build -v \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X main.Version=${pkgver}" .
}

check() {
  cd ${pkgname}-${pkgver}
  go test ./...
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
